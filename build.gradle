plugins {
    id 'java'
    id "org.sonarqube" version "5.1.0.4882"
}

group = 'fr.stan1712.wetston'
version = '2.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url 'https://jitpack.io'
    }
    mavenLocal()
}

sonar {
    properties {
        property "sonar.projectKey", "wetston_FireEquipment"
        property "sonar.projectName", "FireEquipment"
    }
}

project.tasks["sonar"].dependsOn("test", "build")

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.34'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.34'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register('copyJar', Copy) {
    from "build/libs/" + rootProject.name + "-" + version + ".jar"
    into 'J:/@MINECRAFT/SERV LOCAL DEV/plugins'
}
project.tasks["jar"].finalizedBy("copyJar")